//@version=6
indicator("Correction Table", overlay=true)

// Parameter Settings
ath_lookback = input.int(90, "ATH Calculation Period (Days)", minval=1)

// Calculate 200-day Moving Average
ma200 = request.security(syminfo.tickerid, "D", ta.sma(close, 200))

// Calculate difference between current price and MA200
price_diff = ma200 - close
price_diff_percent = (price_diff / ma200) * 100

// Calculate ATH
allTimeHigh(source) =>
    var max = source
    max := math.max(max, source)
ath = request.security(syminfo.tickerid, "1D", allTimeHigh(high))

// Calculate correction reference prices (ATH based)
adj_10 = ath * 0.9  // -10%
adj_20 = ath * 0.8  // -20%
adj_30 = ath * 0.7  // -30%
adj_40 = ath * 0.6  // -40%
adj_50 = ath * 0.5  // -50%

// Create table
var table ma_table = table.new(position.top_right, 4, 8, bgcolor=color.rgb(0, 0, 0, 80))

// Function to update table rows
updateTableRow(table_id, row, label, price, current_price) =>
    price_diff = price - current_price
    diff_percent = (price_diff / current_price) * 100
    table.cell(table_id, 0, row, label, text_color=color.white, text_size=size.normal)
    table.cell(table_id, 1, row, str.tostring(price, "#.##"), text_color=color.white, text_size=size.normal)
    table.cell(table_id, 2, row, str.tostring(price_diff, "#.##"), text_color = price_diff >= 0 ? color.green : color.red, text_size=size.normal)
    table.cell(table_id, 3, row, str.tostring(diff_percent, "#.##") + "%", text_color = price_diff >= 0 ? color.green : color.red, text_size=size.normal)

// Update table every bar
if barstate.islast
    // Header
    table.cell(ma_table, 1, 0, "Price", text_color=color.white, text_size=size.normal)
    table.cell(ma_table, 2, 0, "Deviation", text_color=color.white, text_size=size.normal)
    table.cell(ma_table, 3, 0, "Deviation %", text_color=color.white, text_size=size.normal)

    // Update each row
    updateTableRow(ma_table, 1, "200SMA", ma200, close)
    updateTableRow(ma_table, 2, "ATH", ath, close)
    updateTableRow(ma_table, 3, "ATH-10%", adj_10, close)
    updateTableRow(ma_table, 4, "ATH-20%", adj_20, close)
    updateTableRow(ma_table, 5, "ATH-30%", adj_30, close)
    updateTableRow(ma_table, 6, "ATH-40%", adj_40, close)
    updateTableRow(ma_table, 7, "ATH-50%", adj_50, close)
